plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'kotlin-parcelize'
    id "org.jetbrains.dokka" version "1.5.31"
}

ext {
    kotlin_version = '1.5.31'
    actionTrans = ".action.TRANSACTION"
    actionStatusesRequest = ".action.TRANSACTION_STATUS_REQUEST"
    actionInfoRequest = ".action.INFO"
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 25
        targetSdkVersion 31
        versionCode 20
        versionName "1.2.0"
        buildConfigField "String", "VERSION_NAME", "\"${defaultConfig.versionName}\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        setProperty("archivesBaseName", "androidposintegration-$versionName")
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            def intentPrefix = "com.payplaza"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "ACTION_TRANSACTION", "\"${intentPrefix}${actionTrans}\""
            buildConfigField "String", "ACTION_STATUSES", "\"${intentPrefix}${actionStatusesRequest}\""
            buildConfigField "String", "ACTION_INFO", "\"${intentPrefix}${actionInfoRequest}\""

          }
        debug {
            debuggable true
            def env = ".dev"
            def intentPrefix = "com.payplaza"
            buildConfigField "String", "ACTION_TRANSACTION", "\"${intentPrefix}${env}${actionTrans}\""
            buildConfigField "String", "ACTION_STATUSES", "\"${intentPrefix}${env}${actionStatusesRequest}\""
            buildConfigField "String", "ACTION_INFO", "\"${intentPrefix}${env}${actionInfoRequest}\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

task copyApks(type: Copy) {
    from("$projectDir/build/outputs/apk")
    include '**/*.apk'
    into "$rootDir/artifacts"
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.activity:activity-ktx:1.4.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation('androidx.test:runner:1.4.0')
    androidTestImplementation('androidx.test:rules:1.4.0')
    androidTestImplementation('androidx.test.espresso:espresso-intents:3.4.0')
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.code.gson:gson:2.9.0'
}

afterEvaluate {
    publishing {
        publications {
            mavenAar(MavenPublication) {
                groupId = "com.cm.payments"
                artifactId = rootProject.name
                version = project.android.defaultConfig.versionName
                artifact bundleReleaseAar
            }

            mavenAarDebug(MavenPublication) {
                groupId = "com.cm.payments"
                artifactId = "${rootProject.name}-debug"
                version = project.android.defaultConfig.versionName
                artifact bundleDebugAar
            }
        }
    }
}